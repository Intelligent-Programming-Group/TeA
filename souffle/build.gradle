plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
}

group = 'com.neuromancer42.tea'
version = '0.1.0'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation project(':core')
}

tasks.named('test') {
    useJUnitPlatform()
}

ext {
    swigJavaPackage = 'com.neuromancer42.tea.souffle.swig'
    swigJavaSubdir = 'com/neuromancer42/tea/souffle/swig'
}

tasks.register("genSWIG", SourceTask) {
    description "Generates swig java bindings of c++ code"
    ext.srcDir = file("swig")
    ext.generatedDir = new File(buildDir, 'generated-java')
    ext.dstDir = new File("${ext.generatedDir}/${swigJavaSubdir}")
    it.source ext.srcDir
    it.outputs.dir ext.generatedDir
    it.doLast {
        mkdir "${ext.generatedDir}/${swigJavaSubdir}"
        exec {
            workingDir ext.generatedDir
            commandLine "swig", "-c++", "-java",
                    "-package", "${swigJavaPackage}",
                    "-outdir", "${ext.dstDir}",
                    "-o", "${ext.dstDir}/souffle-swig-interface_wrap.cxx",
                    "${ext.srcDir}/souffle-swig-interface.i"
        }
        copy {
            from ext.srcDir
            into ext.dstDir
            include "CMakeLists.txt"
            include "*.h"
        }
    }
}

tasks.named("compileJava") {
    configure { dependsOn tasks.named("genSWIG") }
}

tasks.named("processResources") {
    configure { dependsOn(tasks.named("genSWIG"))}
    from (genSWIG.ext.generatedDir) {
        include "**/*.cxx"
        include "**/*.h"
        include "**/CMakeLists.txt"
    }
}

sourceSets.main {
    java.srcDir genSWIG.ext.generatedDir
}