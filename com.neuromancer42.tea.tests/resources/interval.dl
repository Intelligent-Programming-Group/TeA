.type M <: unsigned
.type V <: unsigned
.type H <: unsigned
.type F <: unsigned
.type I <: unsigned
.type Z <: unsigned
.type C <: unsigned

.decl ci_pt(v:V, h:H)
.input ci_pt
.decl ci_hpt(h:H, o:H)
.input ci_hpt
.decl ci_fpt(h:H, f:F, o:H)
.input ci_fpt

.decl ci_IM(i:I, m:M)
.input ci_IM

.decl ci_reachableM(m:M)
.input ci_reachableM
.decl MV(m:M, v:V)
.input MV
.decl MI(m:M, i:I)
.input MI

.decl Vvalue(v:V, c:C)
.input Vvalue

.decl LoadPtr(y:V, x:V)
.input LoadPtr
.decl StorePtr(y:V, x:V)
.input StorePtr

.decl LoadFld(y:V, x:V, f:F)
.input LoadFld
.decl StoreFld(y:V, f:F, x:V)
.input StoreFld

.decl IinvkArg(i:I, n:Z, x:V)
.input IinvkArg
.decl IinvkRet(i:I, y:V)
.input IinvkRet
.decl MmethArg(m:M, n:Z, x:V)
.input MmethArg
.decl MmethRet(m:M, y:V)
.input MmethRet

.decl ci_Hval(h:H, c:C)
.output ci_Hval
.decl ci_Vval(v:V, c:C)
.output ci_Vval
.decl ci_Fval(h:H, f:F, c:C)
.output ci_Fval

ci_Vval(v,c) :- ci_reachableM(m), MV(m, v), Vvalue(v,c).

ci_Hval(h,c) :- StorePtr(u,v), ci_pt(u,h), ci_Vval(v,c).
ci_Vval(u,c) :- LoadPtr(u,v), ci_pt(v,h), ci_Hval(h,c).
ci_Fval(h,f,c) :- StoreFld(u,f,v), ci_pt(u,h), ci_Vval(v,c).
ci_Vval(u,c) :- LoadFld(u,v,f), ci_pt(v,h), ci_Fval(h,f,c).

ci_Vval(u,c) :- ci_IM(i,m), IinvkArg(i,z,v), MmethArg(m,z,u), ci_Vval(v,c).
ci_Vval(v,c) :- ci_IM(i,m), IinvkRet(i,v), MmethRet(m,u), ci_Vval(u,c).


