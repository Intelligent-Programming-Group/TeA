# Author: Yifan Chen
# name=java-ci-interval-dlog

# program
#.include "M.dom"
.include "LOC.dom"
.include "ITV.dom"
.include "OP.dom"
.include "IU.dom"
.include "IP.dom"

# BDD variable order
.bddvarorder LOC0xLOC1_IU0xIU1xIU2_IP0_OP0_ITV0xITV1xITV2

# INPUT relations
# inputs & definitions
DefineLoc(v:IU0,p:LOC0,a:ITV0) input
InputLoc(v:IU0,p:LOC0,a:ITV0) input

# assignments
AssignPrimLoc(p:LOC0,v:IU0,u:IU1) input
BinopLoc(p:LOC0,v:IU0,op:OP0,u1:IU1,u2:IU2) input
BinopLoc1(p:LOC0,v:IU0,op:OP0,b1:ITV0,u2:IU1) input
BinopLoc2(p:LOC0,v:IU0,op:OP0,u1:IU1,b2:ITV0) input

# control-flow
PPdirect(p:LOC0,q:LOC1) input
PPcond(p:LOC0,q:LOC1,cond:IP0,v:IU0) input
PPmay(p:LOC0,q:LOC1) input

# arithmetics
Compute(op:OP0,b1:ITV0,b2:ITV1,a:ITV2) input
MaySat(cond:IP0,a:ITV0) input

# auxiliary relations
untouch(p:LOC0,v:IU0)
touch(p:LOC0,v:IU0)

# result
varLocRange(v:IU0,p:LOC0,a:ITV0) output

# RULES ==============
# Note: varLocRange refers to the post-interval of stmt p, i.e. pre-interval of next stmt q.
# definitions
#varLocRange(v,p,a) :- MPentry(m,p), ParamRange(v,a).
varLocRange(v,q,a) :- DefineLoc(v,p,a), PPdirect(p,q).
varLocRange(v,q,a) :- InputLoc(v,p,a), PPdirect(p,q).

# assignments: v := u
varLocRange(v,q,a) :- AssignPrimLoc(p,v,u), PPdirect(p,q), varLocRange(u,p,a).

# binop: v := u1 op u2
varLocRange(v,q,a) :- BinopLoc(p,v,op,u1,u2), PPdirect(p,q), varLocRange(u1,p,b1), varLocRange(u2,p,b2), Compute(op,b1,b2,a).
varLocRange(v,q,a) :- BinopLoc1(p,v,op,b1,u2), PPdirect(p,q), varLocRange(u2,p,b2), Compute(op,b1,b2,a).
varLocRange(v,q,a) :- BinopLoc2(p,v,op,u1,b2), PPdirect(p,q), varLocRange(u1,p,b1), Compute(op,b1,b2,a).

# branch: if cond(v) goto
varLocRange(v,q,a) :- PPcond(p,q,cond,v), varLocRange(v,p,a), MaySat(cond,a).
varLocRange(v,q,a) :- PPmay(p,q), varLocRange(v,p,a).

# otherwise, skip
touch(p,v) :- DefineLoc(v,p,_).
touch(p,v) :- InputLoc(v,p,_).
touch(p,v) :- AssignPrimLoc(p,v,_).
touch(p,v) :- BinopLoc(p,v,_,_,_).
touch(p,v) :- BinopLoc1(p,v,_,_,_).
touch(p,v) :- BinopLoc2(p,v,_,_,_).
untouch(p,v) :- PPdirect(p,_), !touch(p,v).
untouch(p,v) :- PPcond(p,_,_,_), !PPcond(p,_,_,v).
varLocRange(v,q,a) :- untouch(p,v), PPdirect(p,q), varLocRange(v,p,a).
varLocRange(v,q,a) :- untouch(p,v), PPcond(p,q,_,_), varLocRange(v,p,a).
